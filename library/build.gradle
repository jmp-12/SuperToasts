apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'org.skynetsoftware'
version = '2.0.0'

//noinspection GroovyMissingReturnStatement
android {
    compileSdkVersion 24
    buildToolsVersion '24.0.3'

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 20
        versionName "2.0"
    }

}

dependencies {
    archives "org.apache.maven.wagon:wagon-ssh:2.2"
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.android.support:support-annotations:24.0.0'
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    failOnError false
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            configuration = configurations.archives
            repository(url: "scp://maven.android-forever.com/var/www/vhosts/pedjaapps.net/maven.android-forever.com") {
                authentication(userName: androidforeverSshUsername, privateKey: androidforeverSshPrivateKey, passphrase: androidforeverSshPassphrase)
            }

            pom.project {
                artifactId 'supertoasts'
                name 'supertoasts'
                packaging 'aar'
                description 'supertoasts'
                url 'http://github.com/pedja1/SuperToasts'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'pedja1'
                        name 'Predrag ÄŒokulov'
                    }
                }
            }
        }
    }
}
